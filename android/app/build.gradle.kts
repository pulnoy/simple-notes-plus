plugins {
    alias(libs.plugins.android.application)
    alias(libs.plugins.kotlin.android)
    alias(libs.plugins.kotlin.compose)  // v1.5.0: Jetpack Compose Compiler
    alias(libs.plugins.ktlint)  // ✅ v1.6.1: Reaktiviert nach Code-Cleanup
    alias(libs.plugins.detekt)
}

import java.util.Properties
import java.io.FileInputStream
import java.text.SimpleDateFormat
import java.util.Date
import java.util.Locale

android {
    namespace = "dev.dettmer.simplenotes"
    compileSdk = 36

    defaultConfig {
        // Fork multimédia installable à côté de l'application officielle.
        applicationId = "fr.mswgillian.simplenoteskeep"
        minSdk = 24
        targetSdk = 36
        versionCode = 61  // 🆕 v2.18.0 - markdown tables, folder sorting, backup hardening
        versionName = "0.1.0"  // Fork multimédia : première version de test

        // APK-Size: nur tatsächlich gepflegte Locales ausliefern. AndroidX/Material/
        // Compose schleppen sonst ~70+ Sprachvarianten in resources.arsc mit. Geräte
        // mit nicht gelisteten Locales fallen wie gewohnt auf den Default (en) zurück.
        // Liste muss synchron zu res/xml/locales_config.xml gehalten werden.
        // Aufnahme erst ab >= 40% Übersetzungsgrad (Stand v2.14.0: pt-rBR 32%, nl 9%,
        // hi 4%, et 2% noch draußen — bleiben in Weblate und kommen rein, sobald sie
        // die Schwelle reißen).
        androidResources {
            localeFilters += listOf(
                "en", "de", "es", "fr", "in", "it", "nb-rNO", "pl", "ru", "tr", "uk", "zh-rCN",
            )
        }

        testInstrumentationRunner = "androidx.test.runner.AndroidJUnitRunner"
        // 🆕 v2.4.0: Diagnostic phase finished (long-run log validated all v2.4.0 fixes).
        // Default to OFF so new installs/updates don't write sync_debug.log unless the
        // user explicitly enables it via Debug & Diagnose settings.
        buildConfigField("boolean", "SYNC_DEBUG_LOGGING_DEFAULT", "false")

        // 🆕 v2.14.0: Kennzeichnet den `beta`-Build-Typ (Play-Testtracks). Schaltet
        // Diagnose-Defaults vor, damit Tester nichts suchen müssen — siehe buildTypes.
        buildConfigField("boolean", "BETA_BUILD", "false")

        // Debug-Builds unterscheidbar machen: welcher Build läuft beim Tester?
        // (Beta-Feedback: alle Builds hießen "2.11.0-debug (44)"). In "Über diese App"
        // angezeigt für Debug- und (seit v2.14.0) Beta-Builds — bei Tester-Meldungen aus
        // einem Play-Testtrack ist sonst nicht erkennbar, welcher Build lief. Build-Zeit
        // wird bei jeder Konfiguration frisch ausgewertet — für Diagnose gewollt.
        val gitHash = runCatching {
            ProcessBuilder("git", "rev-parse", "--short", "HEAD")
                .directory(rootDir).start()
                .inputStream.bufferedReader().readText().trim()
        }.getOrNull()?.ifEmpty { null } ?: "unknown"
        val buildTime = SimpleDateFormat("yyyy-MM-dd HH:mm", Locale.US).format(Date())
        buildConfigField("String", "GIT_HASH", "\"$gitHash\"")
        buildConfigField("String", "BUILD_TIME", "\"$buildTime\"")
    }
    
    // Disable Google dependency metadata for F-Droid/IzzyOnDroid compatibility
    dependenciesInfo {
        includeInApk = false  // Removes DEPENDENCY_INFO_BLOCK from APK
        includeInBundle = false  // Also disable for AAB (Google Play)
    }
    
    // Product Flavors for F-Droid and standard builds
    // Note: APK splits are disabled to ensure single APK output
    flavorDimensions += "distribution"
    productFlavors {
        create("fdroid") {
            dimension = "distribution"
            // F-Droid builds: currently identical to standard.
            // Flavor exists for future separation (e.g., removing
            // proprietary dependencies from standard builds).
        }
        
        create("standard") {
            dimension = "distribution"
            // Standard builds: currently identical to fdroid.
            // Reserved for Google Play specific features if needed.
        }
    }

    // Signing configuration for release builds
    signingConfigs {
        create("release") {
            // Load keystore configuration from key.properties file
            val keystorePropertiesFile = rootProject.file("key.properties")
            if (keystorePropertiesFile.exists()) {
                val keystoreProperties = Properties()
                keystoreProperties.load(FileInputStream(keystorePropertiesFile))
                
                storeFile = file(keystoreProperties.getProperty("storeFile"))
                storePassword = keystoreProperties.getProperty("storePassword")
                keyAlias = keystoreProperties.getProperty("keyAlias")
                keyPassword = keystoreProperties.getProperty("keyPassword")
            }
        }
    }

    buildTypes {
        debug {
            // ⚡ v1.3.1: Debug-Builds können parallel zur Release-App installiert werden
            applicationIdSuffix = ".debug"
            versionNameSuffix = "-debug"
            isDebuggable = true
        }
        
        release {
            isMinifyEnabled = true
            isShrinkResources = true
            proguardFiles(
                getDefaultProguardFile("proguard-android-optimize.txt"),
                "proguard-rules.pro"
            )
            // Use release signing config if available, otherwise debug
            signingConfig = if (rootProject.file("key.properties").exists()) {
                signingConfigs.getByName("release")
            } else {
                signingConfigs.getByName("debug")
            }
        }

        // 🆕 v2.14.0: Build-Typ für die Play-Testtracks (internal/alpha/beta).
        //
        // Identisch zu `release` — gleiche applicationId, gleicher Signing-Key, gleiches R8 —
        // damit ein Tester-Build sich genau so verhält wie das spätere Production-Release und
        // In-Place-Updates in beide Richtungen funktionieren. Einziger Unterschied: BETA_BUILD
        // schaltet die Diagnose-Defaults vor (Logging an, Entwickleroptionen offen), damit
        // Tester für einen Sync-Log nicht erst 5× auf das App-Banner tippen müssen.
        //
        // Bewusst KEIN applicationIdSuffix: Play akzeptiert pro Eintrag nur eine applicationId,
        // und die Testtracks sind derselbe Eintrag wie Production.
        create("beta") {
            initWith(getByName("release"))
            matchingFallbacks += "release"
            versionNameSuffix = "-beta"
            buildConfigField("boolean", "BETA_BUILD", "true")
            buildConfigField("boolean", "SYNC_DEBUG_LOGGING_DEFAULT", "true")
        }
    }

    // F-Droid liefert keine Testtracks aus — die Variante würde nur die Build- und Testmatrix
    // verdoppeln. Nur `standardBeta` wird gebaut.
    androidComponents {
        beforeVariants(
            selector().withBuildType("beta").withFlavor("distribution" to "fdroid")
        ) { variant ->
            variant.enable = false
        }
    }

    buildFeatures {
        buildConfig = true  // Enable BuildConfig generation
        compose = true  // v1.5.0: Jetpack Compose für Settings Redesign
    }

    // v2.1.0: Remove debug artifacts from release APK
    // v2.2.0: kotlin_builtins direct path fix + LICENSE exclusion
    packaging {
        resources {
            excludes += setOf(
                "DebugProbesKt.bin",
                "kotlin-tooling-metadata.json",
                "kotlin/**/*.kotlin_builtins",
                "kotlin/*.kotlin_builtins",      // direct path (** may not match zero dirs)
                "META-INF/*.kotlin_module",
                "META-INF/versions/**",
                "META-INF/**/LICENSE.txt",        // androidx license copies
                "META-INF/**/LICENSE",
                "META-INF/**/NOTICE.txt",
                "META-INF/**/NOTICE",
                // v2.5.x: Buildzeit-only Artefakte ohne Runtime-Bedeutung
                "META-INF/proguard/**",           // Consumer-Rules (zur Buildzeit konsumiert)
                "META-INF/com.android.tools/**",  // R8/AGP-spezifische Hints
                "META-INF/*.version",             // AndroidX/Kotlin-Versionsmarker
                "META-INF/androidx/**",           // andere AndroidX-Metadaten
                "**/*.kotlin_metadata",           // Kotlin Reflection (nicht genutzt)
            )
        }
    }

    // v1.7.0: Mock Android framework classes in unit tests (Log, etc.)
    testOptions {
        unitTests.isReturnDefaultValues = true
    }
    
    // v1.5.0 Hotfix: Strong Skipping Mode für bessere 120Hz Performance
    // v1.6.1: Feature ist ab dieser Kotlin/Compose Version bereits Standard
    // composeCompiler { }

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_11
        targetCompatibility = JavaVersion.VERSION_11
    }

    lint {
        baseline = file("lint-baseline.xml")
    }
}

kotlin {
    compilerOptions {
        jvmTarget.set(org.jetbrains.kotlin.gradle.dsl.JvmTarget.JVM_11)
    }
}

dependencies {
    // Existing (bleiben so)
    implementation(libs.androidx.core.ktx)
    implementation(libs.androidx.appcompat)
    implementation(libs.material)
    implementation(libs.androidx.activity)

    // Splash Screen API (Android 12+)
    implementation(libs.androidx.core.splashscreen)

    // WebDAV — eigener Mini-Client (sync/webdav/) auf OkHttp, Basic + Digest Auth
    implementation(libs.okhttp)
    implementation(libs.okhttp.digest)
    implementation(libs.kotlinx.coroutines.android)
    implementation(libs.kotlinx.coroutines.core)
    implementation(libs.gson)
    implementation(libs.androidx.work.runtime.ktx)
    implementation(libs.androidx.lifecycle.runtime.ktx)

    // 🔐 v1.7.0: AndroidX Security Crypto. Seit v2.17.0 (WP-3) nur noch Migrationspfad —
    // CredentialStore liest den alten Tink-Store, verschlüsselt selbst per JCA (AES/GCM).
    // Raus in v2.20.0 (deprecated 2025). EncryptionManager nutzt ohnehin reines JCA, nicht diese Lib.
    implementation(libs.androidx.security.crypto)

    // ═══════════════════════════════════════════════════════════════════════
    // v1.5.0: Jetpack Compose für Settings Redesign
    // ═══════════════════════════════════════════════════════════════════════
    implementation(platform(libs.androidx.compose.bom))
    implementation(libs.androidx.compose.ui)
    implementation(libs.androidx.compose.ui.graphics)
    implementation(libs.androidx.compose.ui.tooling.preview)
    implementation(libs.androidx.compose.material3)
    implementation(libs.androidx.compose.material.icons)
    implementation(libs.androidx.activity.compose)
    implementation(libs.androidx.navigation.compose)
    implementation(libs.androidx.lifecycle.runtime.compose)
    debugImplementation(libs.androidx.compose.ui.tooling)

    // ═══════════════════════════════════════════════════════════════════════
    // 🆕 v1.8.0: Homescreen Widgets
    // ═══════════════════════════════════════════════════════════════════════
    implementation(libs.androidx.glance.appwidget)
    implementation(libs.androidx.glance.material3)

    // v2.10.0: Biometric app lock
    implementation(libs.androidx.biometric)
    implementation(libs.androidx.lifecycle.process)

    // 🆕 Bild-Attachments: EXIF-Orientation lesen, Coil für Markdown-Preview-Rendering
    implementation(libs.androidx.exifinterface)
    implementation(libs.coil.compose)

    // Testing (bleiben so)
    testImplementation(libs.junit)
    testImplementation(libs.mockk)
    testImplementation(libs.kotlinx.coroutines.test)
    testImplementation(libs.turbine)
    testImplementation(libs.androidx.arch.core.testing)
    testImplementation(libs.okhttp.mockwebserver)
    // org.json ist im android.jar der Unit-Tests nur gestubbt (jede Methode wirft).
    // Die echte Implementierung auf dem Test-Classpath macht DeletionTracker-JSON testbar.
    testImplementation(libs.org.json)
    androidTestImplementation(libs.androidx.junit)
    androidTestImplementation(libs.androidx.espresso.core)
    androidTestImplementation(platform(libs.androidx.compose.bom))
    androidTestImplementation(libs.androidx.compose.ui.test.junit4)
    debugImplementation(libs.androidx.compose.ui.test.manifest)
}

// ✅ v1.6.1: ktlint reaktiviert nach Code-Cleanup
ktlint {
    android = true
    outputToConsole = true
    ignoreFailures = false
    enableExperimentalRules = false
    
    filter {
        exclude("**/generated/**")
        exclude("**/build/**")
        exclude("**/*.kts")
    }
}

// ⚡ v1.3.1: detekt-Konfiguration
detekt {
    buildUponDefaultConfig = true
    allRules = false
    config.setFrom(files("$rootDir/config/detekt/detekt.yml"))
    baseline = file("$rootDir/config/detekt/baseline.xml")
    
    // Parallel-Verarbeitung für schnellere Checks
    parallel = true
}

// 📋 v1.8.0: Copy F-Droid changelogs to assets for post-update dialog
// Single source of truth: F-Droid changelogs are reused in the app
tasks.register<Copy>("copyChangelogsToAssets") {
    description = "Copies F-Droid changelogs to app assets for post-update dialog"
    
    from("$rootDir/../fastlane/metadata/android") {
        include("*/changelogs/*.txt")
    }
    
    into("$projectDir/src/main/assets/changelogs")
    
    // Preserve directory structure: en-US/20.txt, de-DE/20.txt
    eachFile {
        val parts = relativePath.segments
        if (parts.size >= 3) {
            // parts[0] = locale (en-US, de-DE)
            // parts[1] = "changelogs"
            // parts[2] = version file (20.txt)
            relativePath = RelativePath(true, parts[0], parts[2])
        }
    }
    
    includeEmptyDirs = false

    doLast {
        val versionCode = android.defaultConfig.versionCode ?: return@doLast
        listOf("en-US", "de-DE").forEach { locale ->
            val destDir = file("$projectDir/src/main/assets/changelogs/$locale")
            val target = File(destDir, "$versionCode.txt")
            if (!target.exists()) {
                val latest = destDir.listFiles()
                    ?.filter { it.extension == "txt" }
                    ?.maxByOrNull { it.nameWithoutExtension.toIntOrNull() ?: 0 }
                if (latest != null) {
                    latest.copyTo(target, overwrite = true)
                    logger.warn("copyChangelogsToAssets: $versionCode.txt missing in $locale — copied ${latest.name} as fallback")
                }
            }
        }
    }
}

val copyContributorsToAssets by tasks.registering(Copy::class) {
    from("$rootDir/contributors.json")
    into("$projectDir/src/main/assets")
}

// ponytail: In-App nur die letzten N Versionen bündeln; volle Historie bleibt im
// Repo-CHANGELOG.md und in den GitHub-Releases. Spart ~58 KB APK (40 → N Versionen).
val changelogVersionsInApp = 15
val copyFullChangelogToAssets by tasks.registering {
    description = "Copies the last $changelogVersionsInApp CHANGELOG versions to app assets for in-app display"
    doLast {
        listOf("CHANGELOG.md" to "changelog.md", "CHANGELOG.de.md" to "changelog.de.md")
            .forEach { (src, dst) ->
                val parts = file("$rootDir/../$src").readText().split(Regex("(?m)^## "))
                val trimmed = parts.drop(1).take(changelogVersionsInApp)
                    .joinToString("") { "## $it" }
                file("$projectDir/src/main/assets/$dst")
                    .apply { parentFile.mkdirs() }.writeText(trimmed)
            }
    }
}

// Run before preBuild to ensure changelogs are available
tasks.named("preBuild") {
    dependsOn("copyChangelogsToAssets")
    dependsOn(copyFullChangelogToAssets)
    dependsOn(copyContributorsToAssets)
}
